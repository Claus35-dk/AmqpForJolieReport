\section{Background}
In this chapter we will give some background knowledge about the technologies we worked with: The service oriented programming language Jolie and the wire-level Advanced Message Queue Protocol.
\subsection{Service-oriented architectures}
As mention before Jolie is a service oriented programming language. Service oriented programs does not expose an interface of functions or object-methods but an interface of service-operations. Each service invokes other service's operations through standardized communication protocols. W3C defines a web service as

\blockquote{A software system designed to support interoperable machine-to-machine interaction over a network. It has an interface described in a machine-processable format (specifically WSDL). Other systems interact with the Web service in a manner prescribed by its description using SOAP messages, typically conveyed using HTTP with an XML serialization in conjunction with other Web-related standards.\cite[W3C, 2004]{W3COnWebServices}}

Service-oriented architectures are easy to modularize and they are very scalable. The easy modularization and scalability of service-oriented architectures makes them ideal for distributed systems.
\subsection{Jolie}
Jolie\cite[Jolie-lang.org]{Jolie} is an interpreted language running on the Java Virtual Machine. Jolie makes it extremely easy to host a web service and invoke others.

Jolie supports a growing number of protocols and Jolie promise to keep behavior and deployment separated. What this means is that the code the programmer writes does not need to be changed when the programmer changes protocol. One simply choose another Jolie supported protocol and the program functions as always. This proved a challenge for us in extending the language while still keeping the promise of behavior and deployment separation, mainly because AMQP is not suitable for return procedure calls and because we wanted to add some AMQP specific details in regard to the communication ports.
\subsubsection{A Jolie example}
Jolie makes it extremely easy to set up a service or creating a program which invokes services. We will here give an example of a small Jolie program just to show the basic Jolie features. It should be noted that Jolie can do much more than what we show here.

\noindent\textbf{Communication ports}\\
In Jolie you declare two types of communication ports for exchange of data: Input ports and output ports. Declared input ports are exposed for other services to invoke. Output ports can be used to invoke external services. The two types of communication ports are declared very much alike. A communication port is declared with three elements: Location, protocol and interfaces.

\begin{center}
  \begin{tabular}[H]{p{1cm} l l | p{1cm} l l}
    \multicolumn{3}{l}{inputPort id \{} & \multicolumn{3}{l}{outputPort id \{}\\
    & Location: & URI & & Location: & URI\\
    & Protocol: & p & & Protocol: & p\\
    & Interfaces: & i1, & & Interfaces: & i1,\\
    & & i2 & & & i2\\
    \} & & & \} &
  \end{tabular}
\end{center}

The id is the name of the port for future use f.ex.:\\
\indent\textit{outputPort FileUpdatedOutput \{}

The location is the server address of the service to exchange data with.\\
The protocol field is the name of the transport protocol to be used. The protocol has to be supported by Jolie.\\
The interfaces is a list of interfaces accessible through the port.

Here is an example of output and input port declarations:

\begin{lstlisting}
inputPort FileEventListenerInput {
  Location: "amqp://claus:admin@192.168.229.3:5672/fileevent?queue=fileeventListener"
  Protocol: svdep
  Interfaces: AmqpInputInterface
}

outputPort FileCreatedOutput {
  Location: "amqp://claus:admin@192.168.229.3:5672/fileevent?exchange=fileCreated"
  Protocol: svdep
  Interfaces: AmqpOutputInterface
}
\end{lstlisting}

\noindent\textbf{Declaring an interface}\\
In an interface you declare the operations that are to expected of its implementers. In input ports interfaces are used to declare the methods other services can invoke on your service. In output ports interfaces are used to declare the methods Jolie can expect to find on the port.

Jolie supports two kinds of operations: OneWay and RequestResponse. OneWay methods takes arguments but returns nothing. RequstResponse methods takes arguments and returns a respond.

Here is an example of an interface declaration:

\begin{lstlisting}
interface AmqpInputInterface {
  RequestResponse: receive(string)(bool)
}
interface AmqpOutputInterface {
  OneWay: send(string)
}
\end{lstlisting}

\noindent\textbf{Program the behavior}\\
Putting the examples together forms something with an input port with an interface and an output port with an interface. Let us split those up into two programs, give them some behavior and let one invoke the other.

Let us first create a listener that writes to the console when a new FileEvent has occurred. We will need to include a Jolie interface \textit{console.iol} before we can write to the console. We will also tell Jolie that we want our service to be able to handle multiple asynchronous invocations with:\\
\indent\textit{execution \{concurrent\}}

The full listener program is no more than 20 lines of code:

\begin{lstlisting}
include "console.iol"

execution {concurrent}

interface AmqpInputInterface {
  RequestResponse: receive(string)(bool)
}

inputPort FileEventListenerInput {
  Location: "amqp://username:password@serverip:port/fileevent?queue=fileeventListener"
  Protocol: svdep
  Interfaces: AmqpInputInterface
}

main {
  receive(message)(ack) {
    println@Console(message)();
    ack = true
  }
}
\end{lstlisting}

The method \textit{receive} will always return \textit{true} (ack = true).

That was simple enough. Let us create a program that invokes the listener with FileEvents which we just send as the path to the files:

\begin{lstlisting}
include "console.iol"

interface AmqpOutputInterface {
  OneWay: send(string)
}

outputPort FileUpdatedOutput {
  Location: "amqp://username:password@serverip:port/fileevent?exchange=fileUpdated"
  Protocol: svdep
  Interfaces: AmqpOutputInterface
}
outputPort FileCreatedOutput {
  Location: "amqp://username:password@serverip:port/fileevent?exchange=fileCreated"
  Protocol: svdep
  Interfaces: AmqpOutputInterface
}
outputPort FileDeletedOutput {
  Location: "amqp://username:password@serverip:port/fileevent?exchange=fileDeleted"
  Protocol: svdep
  Interfaces: AmqpOutputInterface
}

main {
  println@Console("Creating a file.")();
  send@FileCreatedOutput("file.txt");

  println@Console("Changing the file.")();
  send@FileUpdatedOutput("file.txt");

  println@Console("Creating a second file.")();
  send@FileCreatedOutput("file2.txt");

  println@Console("Deleting the two files.")();
  send@FileDeletedOutput("file.txt");
  send@FileDeletedOutput("file2.txt")
}
\end{lstlisting}

In this example the locations are referring to an AMQP server. The latter program is actually broadcasting messages to an AMQP exchange server and the listener is subscribing to messages from a queue on the same server. The queue would need to be configured with a binding to the exchange in order to receive the broadcasts. More about that in the next section.
\subsection{Advanced Message Queuing Protocol}
Message queues are being more and more used in event-based and message-oriented software architectures. They are ideal for handling large amounts of data because you can simply queue something for processing and handle the flow of data as quickly as you can. AMQP\cite{AMQP} is an open standard application layer protocol for message-oriented middleware.

AMQP's defining features include message orientation, queuing, routing, security and reliability.

An important thing to know about AMQP is that AMQP is \textit{not} peer-to-peer. Communication between clients and servers goes through an AMQP message queue server which has two mechanisms: Queuing and routing. An AMQP server typically has many exchanges and queues.
\subsubsection{Queues and Exchanges explained}
A \textit{Queue} may bind to one or more exchanges and from the exchange it receives messages to hold.\\
A message handler is called a \textit{Consumer}. A consumer consumes messages by subscribing to a single queue. Multiple consumers may subscribe to the same queue. This is often used to balance work load.

A \textit{Publisher} publishes messages to an \textit{Exchange}. A message can have a \textit{routing key} to help the exchange route the message to the appropriate queue(s).\\
An exchange is defined with an exchange type - a routing algorithm. AMQP 0.9.1 describes support for four types\cite{Amqp091References}:
\begin{itemize}
\item Direct
\item Fanout
\item Topic
\item Headers/Match
\end{itemize}
An exchange of the \textbf{Direct} type will route messages to binded queues which match the routing key criteria in a round-robin fashion.\\
\textbf{Fanout} exchanges route message to \textit{all} binded queues no matter the routing key.\\
\textbf{Topic} exchanges route messages to \textit{all} binded queues which match the routing key criteria.\\
A \textbf{Headers/Match} exchange is for routing messages by multiple attributes. The exchange does not consider the routing key. It matches the headers of the message with the queues binded with the same arguments\cite{AmqpConcepts}.
\begin{figure}[H]
  \includegraphics[width=\textwidth]{illustrations/publisher-exchange-queue-consumer.png}
  \caption{AMQP message path\cite{AmqpConcepts}}
\end{figure}
\subsubsection{Return Procedure Call in AMQP}
AMQP is not built for executing calls that are not asynchronous. Every call is by design asynchronous. Furthermore, AMQP does not define communication peer-to-peer, instead it has a server in the middle that handles communication. RPC-like (Return Procedure Call) functionality can be acheived with a little creativity. How this is done is explained in this section.

Because AMQP is not peer-to-peer we do not specify the other end that handles the call, but the AMQP server's queue for the RPC server. Therefore, the RPC server must create a queue for receiving calls, if there it not one already.

When a client wishes to perform an RPC call it will publish a message directly to the RPC server's queue with some extra properties set. The client will then wait for a response, but AMQP does, by design, not support responding to a message, and we therefore need another way of transferring the response back to the client. This is where the creativity comes in.

When a client performs an RPC call it will create a queue to receive the response on. The message published to the RPC server will have the property called `replyTo' set to the name of the queue that the response should be published to.

However, creating a new queue for every call is obviously not very efficient, so AMQP supports another property that can be set on the request. This property is called `correlationId'. It contains a unique ID that is used to match responses with requests. This way we can create a single response queue for each client.

\begin{figure}[H]
  \includegraphics[width=\textwidth]{illustrations/amqp-rpc.png}
  \caption{AMQP RPC model\cite{AMQPRPC}}
\end{figure}

\label{subsubsec:AMQPRPC}
\newpage